// 打包版本号
version = rootProject.ext.version["skillfull-service-modules:skillfull-message-service"]['version']


bootJar { enabled = rootProject.ext.version["skillfull-service-modules:skillfull-message-service"]['bootJar'] }


dependencies {
    implementation project(':skillfull-core-modules:skillfull-mvc')
    implementation project(':skillfull-core-modules:skillfull-oauth2-mvc')
    implementation project(':skillfull-api-modules:skillfull-message-api')
    implementation project(':skillfull-core-modules:skillfull-redis')
    implementation project(':skillfull-core-modules:skillfull-database')
    implementation('org.springframework.boot:spring-boot-starter-websocket') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'com.alibaba.cloud:aliyun-sms-spring-boot-starter'
    implementation rootProject.ext.dependencies["aliyun-java-sdk-core"]
}


// 判断是否输出到统一目录
if (rootProject.ext["compileBootJarToParent"]) {
    tasks.withType(Jar) {
        destinationDirectory = file("$project.parent.buildDir/bootJar")
    }
}


// 判断是否分离打包
if (rootProject.ext["compileSeparate"]) {
    // 分离打包处理
    task clearJar(type: Delete) {
        delete "$buildDir/libs/lib"
    }

    task copyJar(type: Copy) {
        from project.configurations.productionRuntimeClasspath
        into "$buildDir/libs/lib"
        include '*.jar'
        exclude "skillfull*.jar"
    }

    bootJar {
        // 排除外部lib下依赖
        project.configurations.productionRuntimeClasspath.files.collect {
            {
                if (!it.name.startsWith("skillfull")) {
                    exclude(it.name)
                }
            }
        }
        // 打包前清空
        dependsOn clearJar
        dependsOn copyJar
        // 指定依赖包的路径
        manifest {
            attributes "Manifest-Version": 1.0,
                    'Class-Path': project.configurations.productionRuntimeClasspath.files.collect { "lib/$it.name" }.join(' ')
        }
    }
}