bootJar { enabled = true }


dependencies {
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-mvc')
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-oauth2-mvc')
    implementation project(':anyi-cloud-rpc-modules:anyi-cloud-rpc-process')
    implementation project(':anyi-cloud-rpc-modules:anyi-cloud-rpc-system')
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-redis')
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-database')
    implementation rootProject.ext.dependencies["camunda-bpm-spring-boot-starter-webapp"]
    implementation rootProject.ext.dependencies["jython"]
    implementation rootProject.ext.dependencies["jruby-complete"]
    implementation rootProject.ext.dependencies["groovy-all"]
    implementation rootProject.ext.dependencies["seata-spring-boot-starter"]
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-stream')
    implementation project(':anyi-cloud-core-modules:anyi-cloud-core-logging-mvc')
}


// 判断是否输出到统一目录
if (compileBootJarToParent) {
    tasks.withType(Jar) {
        destinationDirectory = file("$project.parent.buildDir/bootJar")
    }
}


// 判断是否分离打包
if (compileSplit) {
    // 分离打包处理
    task clearJar(type: Delete) {
        delete "$buildDir/libs/lib"
    }

    task copyJar(type: Copy) {
        from project.configurations.productionRuntimeClasspath
        into "$buildDir/libs/lib"
        include '*.jar'
        exclude "anyi-cloud-*.jar"
    }

    bootJar {
        // 排除外部lib下依赖
        project.configurations.productionRuntimeClasspath.files.collect {
            {
                if (!it.name.startsWith("anyi-cloud")) {
                    exclude(it.name)
                }
            }
        }
        // 打包前清空
        dependsOn clearJar
        dependsOn copyJar
        // 指定依赖包的路径
        manifest {
            attributes "Manifest-Version": 1.0,
                    'Class-Path': project.configurations.productionRuntimeClasspath.files.collect { "lib/$it.name" }.join(' ')
        }
    }
}
